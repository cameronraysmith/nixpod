# Default command when 'just' is run without arguments
# Run 'just <command>' to execute a command.
default: help

# Display help
help:
    @printf "\nRun 'just -n <command>' to print what would be executed...\n\n"
    @just --list
    @echo "\n...by running 'just <command>'.\n"
    @echo "This message is printed by 'just help' and just 'just'.\n"

# Build and Load the Docker Image with Docker
build:
    docker build -t debnix:latest .

# activate home manager: rm ~/.bashrc ~/.profile || true && nix run && zsh
# Run the Docker container
run mount_path="$(pwd)" work_dir="/home/runner/work": build
    docker run -it --entrypoint "/bin/bash" \
    --rm -v {{mount_path}}:{{work_dir}} debnix:latest \
    -c 'cd {{work_dir}} && echo "export PS1=\"${debian_chroot:+($debian_chroot)}\h:\w\$\n> \"" >> ~/.bashrc && exec bash'

# Get docker image digest
digest:
    docker run -it --rm \
    --entrypoint skopeo quay.io/skopeo/stable \
    inspect docker://docker.io/debian:stable-slim | \
    jq -r .Digest | tr -d '\n'

# Get docker image tarball sha256
sha256:
    docker pull debian:stable-slim
    docker save -o debian_stable_slim.tar debian:stable-slim
    nix-hash --type sha256 --base16 debian_stable_slim.tar
    nix-hash --type sha256 --base32 debian_stable_slim.tar
    nix-hash --type sha256 --base64 debian_stable_slim.tar
    rm debian_stable_slim.tar || true

# Build and Load the Docker Image with nix
buildnix:
    @echo "Building the Docker image..."
    nix build .#dockerImage
    @echo "Loading the Docker image into Docker daemon..."
    docker load < result

# Run the Docker Container built by nix
runnix: buildnix
    @echo "Running the Docker container..."
    docker run -it --rm -v $(pwd):/mnt trynix:latest
